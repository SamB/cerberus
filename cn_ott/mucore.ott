% NB the comments here relate to the Core source syntax; the Lem
% datatypes have additional values used at Core runtime

embed {{ tex-preamble
%\usepackage[scaled=0.82]{beramono}
\renewcommand{\ottkw}[1]{\texttt{#1}%
}
}}


embed {{ lem
open import Core_ctype Annot Loc
import Symbol Implementation_ Undefined Cmm_csem
import Mem Mem_common
import Linux
}}


metavar Symbol_sym {{ tex ident }} {{ lem Symbol.sym }} ::=
  {{ phantom }} {{ com Core identifier }}
metavar tag {{ lem Symbol.sym }} ::=
  {{ phantom }} {{ com struct/union tag }}



indexvar n, i ::= {{ phantom }}

grammar
  % Inhabitant of these types may be read and stored from the memory (hence they match C types)
  object_type {{ tex \textit{oTy} }} :: 'OTy_' ::= {{ com types for C objects}}
    | integer                                                            :: ::  integer
    | floating                                                           :: ::  floating
    | pointer                                                            :: ::  pointer
    | array ( object_type )                                         :: ::  array
    | struct tag                                                         :: ::  struct
    | union tag                                                          :: ::  union

embed {{ lem
let rec eq_object_type oTy1 oTy2 =
  let ord = function
    | OTy_integer ->
        (0: nat)
    | OTy_floating ->
        1
    | OTy_pointer ->
        2
    | OTy_array _ ->
        3
    | OTy_struct _ ->
        4
    | OTy_union _ ->
        5
  end in
  match (oTy1, oTy2) with
    | (OTy_array oTy1, OTy_array oTy2) ->
        eq_object_type oTy1 oTy2
    | (OTy_struct sym1, OTy_struct sym2) ->
        sym1 = sym2
    | (OTy_union sym1, OTy_union sym2) ->
        sym1 = sym2
    | _ ->
        ord oTy1 = ord oTy2
  end
instance (Eq object_type)
  let (=) = eq_object_type
  let (<>) = fun x y -> not (eq_object_type x y)
end
}}

grammar
  base_type {{ tex \textit{bTy} }} :: 'BTy_' ::= {{ com Core base types }}
    | unit                           ::   :: Unit {{ com unit }}
    | bool                           ::   :: Bool {{ com boolean }}
    | ctype                          ::   :: ctype    {{ com Core type of C type exprs}} {{ tex \ottkw{ctype} }}
    | [ base_type ]                  ::   :: list     {{ com list }}
    | ( </ base_typei // , // i /> ) ::   :: tuple    {{ com tuple }}
    | object_type                    ::   :: object   {{ com C object value }}
    | loaded object_type             ::   :: loaded   {{ com [[object_type]] or unspecified }} {{ tex [[loaded]]\ [[object_type]] }}
    | storable                            :: X :: storable {{ com top type for integer/float/pointer/structs (maybe union?). This is only used in the type system }}

embed {{ lem
let rec eq_base_type bTy1 bTy2 =
  let ord = function
    | BTy_unit ->
        (0: nat)
    | BTy_boolean ->
        1
    | BTy_ctype ->
        2
    | BTy_list _ ->
        3
    | BTy_tuple _ ->
        4
    | BTy_object _ ->
        5
    | BTy_loaded _ ->
        6
    | BTy_storable ->
        7
  end in
  match (bTy1, bTy2) with
    | (BTy_list bTy1', BTy_list bTy2') ->
        eq_base_type bTy1' bTy2'
    | (BTy_tuple bTys1, BTy_tuple bTys2) ->
        listEqualBy eq_base_type bTys1 bTys2
    | (BTy_object oTy1, BTy_object oTy2) ->
        oTy1 = oTy2
    | (BTy_loaded oTy1, BTy_loaded oTy2) ->
        oTy1 = oTy2
    | _ ->
        ord bTy1 = ord bTy2
  end
instance (Eq base_type)
  let (=) = eq_base_type
  let (<>) = fun x y -> not (eq_base_type x y)
end
}}


grammar
  % TODO: this isn't really used...
  type {{ tex \textit{coreTy} }} :: 'Ty' ::= {{ com Core types }}
    | base_type     :: :: Base   {{ com pure base type }}
    | eff base_type :: :: Effect {{ com effectful base type }}

  % Binary operators
  binop :: 'Op' ::= {{ com binary operators }}
    % arithmetic operators (integer -> integer -> integer)
    | +     :: :: Add {{ tex \texttt{+} }}
    | -     :: :: Sub {{ tex \texttt{-} }}
    | *     :: :: Mul {{ tex \texttt{*} }}
    | /     :: :: Div {{ tex \texttt{/} }}
    | rem_t :: :: Rem_t % This is the % of C. Hence, rem_t(x,y) = x - (truncate(x/y) * y)
    | rem_f :: :: Rem_f % rem_t(x,y) = x - (foor(x/y) * y)
    | ^     :: :: Exp {{ tex \texttt{\char`^} }}
    % relational operators
    | =     :: :: Eq {{ tex \texttt{=} }} % defined both for integer and ctype
    | >     :: :: Gt {{ tex \texttt{>} }}
    | <     :: :: Lt {{ tex \texttt{<} }}
    | >=    :: :: Ge {{ tex \texttt{>=} }}
    | <=    :: :: Le {{ tex \texttt{<=} }}
    % logical connectives
    | /\    :: :: And {{ tex \texttt{/\char`\\} }}
    | \/    :: :: Or  {{ tex \texttt{\char`\\/} }}

embed {{ lem
instance (Eq binop)
  let (=)  = unsafe_structural_equality
  let (<>) = unsafe_structural_inequality
end

instance (Show binop)
  let show op = 
    match op with
      | OpAdd   -> "OpAdd"
      | OpSub   -> "OpSub"
      | OpMul   -> "OpMul"
      | OpDiv   -> "OpDiv"
      | OpRem_t -> "OpRem_t"
      | OpRem_f -> "OpRem_f"
      | OpExp   -> "OpExp"
      | OpEq    -> "OpEq"
      | OpGt    -> "OpGt"
      | OpLt    -> "OpLt"
      | OpGe    -> "OpGe"
      | OpLe    -> "OpLe"
      | OpAnd   -> "OpAnd"
      | OpOr    -> "OpOr"
    end
  end
}}


grammar
  polarity :: '' ::= {{ com memory action polarities }}
    | Pos :: :: Pos {{ com sequenced by \ottkw{let\ weak} and \ottkw{let\ strong} }}
    | Neg :: :: Neg {{ com only sequenced by \ottkw{let\ strong} }}


metavar
  Implementation__implementation_constant
  {{ lem Implementation_.implementation_constant }}
  {{ tex \ottnt{\texttt{<}impl\textit{-}const\texttt{>} } }} ::=
    {{ phantom }}

metavar x, y, tyvarsym {{ lem 'sym }} {{tex \mathit{ident} }} ::=
  {{ phantom }}

grammar
  mu_name {{ tex name }} :: '' ::= {{ auxparam 'sym }}
    | tyvarsym                                :: :: Sym   {{ com Core identifier }} 
    | Implementation__implementation_constant :: :: Impl  {{ com implementation-defined constant }}

embed {{ lem
instance forall 'sym. MapKeyType 'sym => (MapKeyType (mu_name 'sym))
  let mapKeyCompare nm1 nm2 =
    match (nm1, nm2) with
      | (Sym sym1, Sym sym2) ->
          mapKeyCompare sym1 sym2
      | (Impl iCst1, Impl iCst2) ->
          mapKeyCompare iCst1 iCst2
      | (Sym _, Impl _) ->
          LT
      | (Impl _, Sym _) ->
          GT
    end
end
}}


metavar Mem_integer_value
  {{ tex intval }}
  {{ lem Mem.integer_value }} ::=
    {{ phantom }}  {{ com integer value }}
metavar Mem_floating_value
  {{ tex floatval }}
  {{ lem Mem.floating_value }} ::=
    {{ phantom }}  {{ com floating value }}
%metavar Mem_pointer_value
%  {{ lem Mem.pointer_value }} ::=
%    {{ phantom }}  
metavar Mem_mem_value
  {{ lem Mem.mem_value }}
  {{ tex memval }} ::=
    {{ phantom }}  
metavar Cabs_cabs_identifier
  {{ tex member }}
  {{ lem Cabs.cabs_identifier }} ::=
    {{ phantom }}
    {{ com C struct/union member name }}
metavar ty
  {{ lem ctype }}
  {{ tex \tau }} ::=
    {{ phantom }}

grammar
  Mem_pointer_value {{ tex ptrval }} :: '' ::= {{ phantom }} {{ lem Mem.pointer_value }}
    | nullptr ( ty ) :: :: nullptr

% (* Object values (inhabitants of object types, hence values that can be read/stored) *)
  mu_object_value {{ tex object\_value }}:: 'OV' ::=  {{ auxparam 'sym }} {{ com C object values }} 
    | Mem_integer_value :: :: integer  {{ com integer value }}
    | Mem_floating_value :: :: floating {{ com floating-point value }}
    | Mem_pointer_value :: :: pointer {{ com pointer value }}
    | array ( </ mu_loaded_valuei // , // i /> ) :: :: array {{ com C array value }}
    | ( struct tag ) { </ . Cabs_cabs_identifieri : tyi = Mem_mem_valuei // , // i /> } :: :: struct {{ com C struct value }}
    | ( union tag ) { . Cabs_cabs_identifier = Mem_mem_value } :: :: union {{ com C union value }}
  
  mu_loaded_value {{ tex loaded\_value }} :: 'LV' ::= {{ auxparam 'sym }} {{ com potentially unspecified C object values }}
    | Specified ( mu_object_value ) :: :: specified {{ com non-unspecified loaded value }}
    | Unspecified ( ty ) :: :: unspecified {{ com unspecified loaded value }}
  
  mu_value {{ tex value }} :: 'V' ::= {{ auxparam 'sym }} {{ com Core values }}
    | mu_object_value                                    :: :: object {{ com C object value }} 
    | mu_loaded_value                                    :: :: loaded {{ com loaded C object value }}
    | Unit                                                    :: :: unit
    | True                                                    :: :: true
    | False                                                   :: :: false
    | ' ty '                                                  :: :: ctype {{ com C type as value }}
    | base_type [ mu_value1 , .. , mu_valuei ] :: :: list {{ tex [ value_1 , .. , value_i ] }}
    | ( mu_value1 , .. , mu_valuei )                :: :: tuple {{ com tuple }}


metavar bty {{ lem 'bty }} ::= {{ phantom }}
grammar
  mu_ctor {{ tex ctor }} :: 'C' ::= {{ auxparam 'bty }} {{ com data constructors }}
    | Nil bty     :: :: nil         {{ com empty list }}                     % : [bTy] 
    | Cons        :: :: cons        {{ com list cons }}                      % : bTy -> [bTy] -> [bTy] 
    | Tuple       :: :: tuple       {{ com tuple }}                          % : bTy1 -> ... -> bTyN -> (bTy1, ..., bTyN) 
    | Array       :: :: array       {{ com C array }}                        % : bTy -> ... -> bTy -> array bTy 
    | Ivmax       :: :: ivmax       {{ com max integer value }}              % : ctype -> integer 
    | Ivmin       :: :: ivmin       {{ com min integer value }}              % : ctype -> integer 
    | Ivsizeof    :: :: ivsizeof    {{ com sizeof value }}                   % : ctype -> integer 
    | Ivalignof   :: :: ivalignof   {{ com alignof value }}                  % : ctype -> integer 
    | IvCOMPL     :: :: ivCOMPL     {{ com bitwise complement }}             % : ctype -> integer -> integer
    | IvAND       :: :: ivAND       {{ com bitwise AND }}                    % : ctype -> integer -> integer -> integer
    | IvOR        :: :: ivOR        {{ com bitwise OR }}                     % : ctype -> integer -> integer -> integer
    | IvXOR       :: :: ivXOR       {{ com bitwise XOR }}                    % : ctype -> integer -> integer -> integer
    | Specified   :: :: specified   {{ com non-unspecified loaded value }}   % : objTy -> loaded objTy 
    | Unspecified :: :: unspecified {{ com unspecified loaded value }}       % : ctype -> loaded (integer|floating|pointer|array|struct|union) 
    | Fvfromint   :: :: fvfromint   {{ com cast integer to floating value }} % : integer -> floating
    | Ivfromfloat :: :: ivfromfloat {{ com cast floating to integer value }} % : ctype -> floating -> integer

% TODO: hide from the tex
metavar annots {{ lem list annot }} ::= {{ phantom }}

grammar
  maybesym_base_type :: '' ::= {{ lem (maybe 'sym * base_type) }} {{ phantom }}
    | _ : base_type        :: :: Nothing (+ binders = {} +)
    | tyvarsym : base_type :: :: Just    (+ binders = tyvarsym +)
  
  mu_pattern_aux {{ lem mu_pattern_ }} :: 'Case' ::= {{ auxparam ('bty, 'sym) }}
    | maybesym_base_type                           :: X :: Base % (+ binders = binders(maybesym) +)
    | mu_ctor ( </ mu_patterni // , // i /> ) ::   :: Ctor % (+ b = b( </ mu_pattern_auxi // , // i /> ) +)
  
  mu_pattern :: '' ::= {{ auxparam ('bty, 'sym) }}
    | annots mu_pattern_aux :: :: Pattern % (+ binders = binders(mu_pattern_aux) +)

metavar Mem_mem_iv_constraint
  {{ lem Mem.mem_iv_constraint }} ::=
    {{ phantom }}
metavar Undefined_undefined_behaviour
  {{ tex \ottnt{ub\textit{-}name} }}
  {{ lem Undefined.undefined_behaviour }} ::=
    {{ phantom }} 
metavar string {{ lem string }} ::= {{ phantom }} 

grammar
  mu_pexpr_aux {{ lem mu_pexpr_ }} :: 'PE' ::= {{ auxparam ('bty,'sym) }} {{ com Core pure expressions }}
    | tyvarsym                                                                         ::   :: sym
    | Implementation__implementation_constant                                          ::   :: impl {{ com implementation-defined constant }} 
    | mu_value                                                                    ::   :: val
    | constrained ( </ Mem_mem_iv_constrainti , tyvarsymi // , // i /> )               :: X :: constrained {{ com constrained value }} % TODO: hide from tex
    | undef Loc_t ( Undefined_undefined_behaviour )                                    ::   :: undef {{ com undefined behaviour }}
    | error ( string , tyvarsym )                                                      ::   :: error {{ com impl-defined static error }}
    | mu_ctor ( </ tyvarsymi // , // i /> )                                       ::   :: ctor {{ com data constructor application }}
    | array_shift ( tyvarsym1 , ty , tyvarsym2 )                                       ::   :: array_shift {{ com pointer array shift }}
    | member_shift ( tyvarsym1 , tyvarsym2 , Cabs_cabs_identifier )                    ::   :: member_shift {{ com pointer struct/union member shift }}
    | not ( tyvarsym )                                                                 ::   :: not {{ com boolean not }}
    | tyvarsym1 binop tyvarsym2                                                        ::   :: op {{ order [[binop]] [[tyvarsym1]] [[tyvarsym2]] }}
    | ( struct tyvarsym ) { </ . Cabs_cabs_identifieri = tyvarsymi // , // i /> }      ::   :: struct {{ com C struct expression }}
    | ( union tyvarsym1 ) { . Cabs_cabs_identifier = tyvarsym2 }                       ::   :: union {{ com C union expression }}
    | memberof  ( tyvarsym1 , Cabs_cabs_identifier , tyvarsym2 )                       ::   :: memberof {{ com C struct/union member access }}
    | mu_name ( tyvarsym1 , .. , tyvarsymn )                                      ::   :: call {{ com pure function call }}

    | assert_undef ( tyvarsym , Undefined_undefined_behaviour )                        ::   :: assert_undef
    | bool_to_integer ( tyvarsym )                                                     ::   :: bool_to_integer
    | conv_int ( ty , tyvarsym )                                                       ::   :: conv_int
    | wrapI ( ty , tyvarsym )                                                          ::   :: wrapI
 
  mu_pexpr {{ tex e }} :: '' ::= {{ auxparam ('bty, 'sym) }}
    | annots bty mu_pexpr_aux :: :: Pexpr

  mu_tpexpr {{ lem mu_tpexpr_ }} :: 'PE' ::= {{ auxparam ('bty, 'sym) }} {{ com Core top-level pure expressions }}
    | case tyvarsym of </ | mu_patterni => mu_tpexpri // i /> end            ::   :: case {{ com pattern matching }}
    | let mu_pattern = mu_tpexpr1 in mu_tpexpr2                         ::   :: let {{ com pure let }} % TODO: binders
    | if tyvarsym then mu_tpexpr1 else mu_tpexpr2                            ::   :: if {{ com pure if }}
    | done tyvarsym                                                                    ::   :: done {{ com pure done }}



metavar a {{ tex }} {{ lem 'a }} ::= {{ phantom }}
metavar nat {{ tex n }} {{ lem nat }} ::= {{ phantom }}
metavar bool {{ lem bool }} ::= {{ phantom }}
metavar Loc_t {{ lem Loc.t }} ::= {{ phantom }}
metavar Symbol_prefix {{ tex }} {{ lem Symbol.prefix }} ::= {{ phantom }}
metavar Cmm_memory_order {{ tex \ottnt{memory\textit{-}order} }} {{ lem Cmm_csem.memory_order }} ::= {{ phantom }}
metavar Linux_linux_memory_order {{ tex \ottnt{linux\textit{-}memory\textit{-}order} }} {{ lem Linux.linux_memory_order }} ::= {{ phantom }}
metavar Thread_thread_id {{ tex \ottnt{thread\textit{-}id} }} {{ lem thread_id }} ::= {{ phantom }}


grammar
  mu_action_aux {{ lem mu_action_ }} :: '' ::= {{ auxparam ('bty, 'sym) }} {{ com memory actions }}
    | create ( mu_pexpr1 , mu_pexpr2 ) Symbol_prefix                                                                              :: :: Create
    | create_readonly ( mu_pexpr1 , mu_pexpr2 , mu_pexpr3 ) Symbol_prefix                                                    :: :: CreateReadOnly
    | alloc ( mu_pexpr1 , mu_pexpr2 ) Symbol_prefix                                                                               :: :: Alloc
    | kill ( bool , mu_pexpr )                                                                                                         :: :: Kill {{ com the boolean indicates whether the action is dynamic (i.e. free()) }}
    | store ( bool , mu_pexpr1 , mu_pexpr2 , mu_pexpr3 , Cmm_memory_order )                                                  :: :: Store {{ com the boolean indicates whether the store is locking }}
    | load ( mu_pexpr1 , mu_pexpr2 , Cmm_memory_order )                                                                           :: :: Load
    | rmw ( mu_pexpr1 ,  mu_pexpr2 , mu_pexpr3 , mu_pexpr4 , Cmm_memory_order1 , Cmm_memory_order2 )                    :: :: RMW
    | fence ( Cmm_memory_order )                                                                                                            :: :: Fence
    | compare_exchange_strong ( mu_pexpr1 , mu_pexpr2 , mu_pexpr3 , mu_pexpr4 , Cmm_memory_order1 , Cmm_memory_order2 ) :: :: CompareExchangeStrong
    | compare_exchange_weak ( mu_pexpr1 , mu_pexpr2 , mu_pexpr3 , mu_pexpr4 , Cmm_memory_order1 , Cmm_memory_order2 )   :: :: CompareExchangeWeak
    | linux_fence ( Linux_linux_memory_order )                                                                                              :: :: LinuxFence
    | linux_load ( mu_pexpr1 , mu_pexpr2 , Linux_linux_memory_order )                                                             :: :: LinuxLoad
    | linux_store ( mu_pexpr1 , mu_pexpr2 , mu_pexpr3 , Linux_linux_memory_order )                                            :: :: LinuxStore
    | linux_rmw ( mu_pexpr1 , mu_pexpr2 , mu_pexpr3 , Linux_linux_memory_order )                                             :: :: LinuxRMW
  
  mu_action :: '' ::= {{ auxparam ('a, 'bty, 'sym) }}
    | Loc_t a mu_action_aux :: :: Action
  
  mu_paction :: '' ::= {{ auxparam ('a, 'bty, 'sym) }} {{ com memory actions with polarity }}
    |  polarity mu_action :: X :: Paction
    |  mu_action          :: M :: Positive {{ com positive, sequenced by both \ottkw{let\ weak} and \ottkw{let\ strong} }} {{ ocaml Paction Pos [[mu_action]] }}
    |  neg ( mu_action )  :: M :: Negative {{ com negative, only sequenced by \ottkw{let\ strong} }} {{ ocaml Paction Neg [[mu_action]] }}


%metavar Mem_memop {{ lem Mem.memop }} {{ tex memop }} ::= {{ phantom }}  
grammar
  Mem_memop {{ tex memop }}  {{ lem Mem_common.memop }} :: '' ::= {{ phantom }} {{ com operations involving the memory state}}
    | pointer_equality_operator   :: :: PtrEqNe          {{ tex \textit{pointer-equality-operator} }} {{ com pointer equality comparison}}
    | pointer_relational_operator :: :: PtrLtGtLeGe      {{ tex \textit{pointer-relational-operator} }} {{ com pointer relational comparison}}
    | ptrdiff                     :: :: Ptrdiff          {{ com pointer subtraction }}
    | intFromPtr                  :: :: IntFromPtr       {{ com cast of pointer value to integer value }} 
    | ptrFromInt                  :: :: PtrFromInt       {{ com cast of integer value to pointer value }} 
    | ptrValidForDeref            :: :: PtrValidForDeref {{ com dereferencing validity predicate }}
    | ptrWellAligned              :: :: PtrWellAligned
    | ptrArrayShift               :: :: PtrArrayShift
    | memcpy                      :: :: Memcpy
    | memcmp                      :: :: Memcmp
    | realloc                     :: :: Realloc          {{ com TODO: not sure about this }}
    | va_start                    :: :: Va_start
    | va_copy                     :: :: Va_copy
    | va_arg                      :: :: Va_arg
    | va_end                      :: :: Va_end
  
  tyvarsym_base_type_pair :: '' ::= {{ lem ('sym * base_type) }} {{ phantom }} % TODO: check with P
    | tyvarsym : base_type :: :: Pair
  
  base_type_pexpr_pair :: '' ::= {{ lem (base_type * ('bty, 'sym) mu_pexpr }}  {{ phantom }} % TODO: check with P
    | base_type := mu_pexpr :: :: FOO
  
  
  mu_expr_aux {{ tex E }} {{ lem mu_expr_ }} :: 'E' ::= {{ auxparam ('a, 'bty, 'sym) }} {{ com (effectful) expression }}
    | pure ( mu_pexpr ) :: :: pure
    | memop ( Mem_memop , mu_pexpr1 , .. , mu_pexprn ) :: :: memop {{ com pointer op involving memory }}
    | mu_paction  :: :: action {{ com memory action }}
    | case mu_pexpr with </ | mu_patterni => mu_expri // i /> end :: :: case {{ com pattern matching }} % TODO: bindings
    | let mu_pattern = mu_pexpr in mu_expr :: :: let % TODO: bindings
    | if mu_pexpr then mu_expr1 else mu_expr2 :: :: if
    | skip :: :: skip
    | ccall a ( mu_pexpr1 , mu_pexpr2 , </ mu_pexpri // , // i /> ) :: :: ccall {{ com C function call }}
    | pcall a ( mu_name , </ mu_pexpri // , // i /> ) :: :: proc {{ com Core procedure call }}
    | unseq ( mu_expr1 , .. , mu_exprn ) :: :: unseq {{ com unsequenced expressions }}
    | let weak mu_pattern = mu_expr1 in mu_expr2 :: :: wseq {{ com weak sequencing }} % TODO: bindings
    | let strong mu_pattern = mu_expr1 in mu_expr2 :: :: sseq {{ com strong sequencing }} % TODO: bindings
    | let atomic tyvarsym_base_type_pair = mu_action1 in mu_paction2 :: :: aseq  {{ com atomic sequencing }}% (* this ctor doesn't exist at runtine *)
    | indet [ nat ] ( mu_expr ) :: :: indet  {{ com indeterminately sequenced expr }} % (* this ctor doesn't exist at runtine *)
    | bound [ nat ] ( mu_expr ) :: :: bound  {{ com $\ldots$and boundary }} %(* this ctor doesn't exist at runtine *)
    | nd ( mu_expr1 , .. , mu_exprn ) :: ::  nd {{ com nondeterministic sequencing }}
    | save tyvarsym_base_type_pair ( </ tyvarsymi : base_type_pexpr_pairi // , // i /> ) in mu_expr :: :: save  {{ com save label }}
    | run a tyvarsym ( </ mu_pexpri // , // i /> ) :: :: run  {{ com run from label }}
    | par ( mu_expr1 , .. , mu_exprn ) :: :: par  {{ com cppmem-like thread creation }}
    | wait ( Thread_thread_id ) :: :: wait {{ com wait for thread termination }} %(* TODO: this will need to have a Core type annotation to allow typecheck ... *)
  
   mu_expr {{ tex E }} :: '' ::= {{ auxparam ('a, 'bty, 'sym) }}
     | annots mu_expr_aux :: :: Expr


%grammar
%  % TODO: this wrong, here we want the 'sym of mu_pexpr to be instanciated to Symbol.sym
%  mu_impl_decl :: '' ::= {{ auxparam 'bty }}
%    | base_type := mu_pexpr :: :: Def
%    | base_type ( </ Symbol_symi : base_typei // , // i /> ) := mu_pexpr :: :: IFun



%  definition :: '' ::= {{ phantom }} {{ com Core definitions }}
% %    | fun mu_name ( mu_pexpr1 : base_type1 , .. , mu_pexprn : base_typen ) : base_type := mu_pexpr :: :: Fun    
% %    | proc mu_name ( mu_pexpr1 : base_type1 , .. , mu_pexprn : base_typen ) : eff base_type := mu_expr :: :: Proc
%     | fun mu_name ( </ Symbol_symi : base_typei // , // i /> ) : base_type := mu_pexpr :: :: Fun {{ com Core function definition }}
%     | proc mu_name ( </ Symbol_symi : base_typei // , // i /> ) : eff base_type := mu_expr :: :: Proc {{ com Core procedure definition }}



% terminals :: 'terminal' ::=
%    | /\    :: :: And  {{ tex \wedge }}
%    | \/    :: :: Or   {{ tex \vee }}
%    | ^     :: :: Expr {{ tex \hat{} }}
%    | =>    :: :: Pat  {{ tex \texttt{=>} }}
%    | <     :: :: langle {{ tex \texttt{<} }}
%    | >     :: :: rangle {{ tex \texttt{>} }}
%    | [     :: :: lbrac {{ tex \texttt{[} }}
%    | ]     :: :: rbrac {{ tex \texttt{]} }}
%    | (     :: :: lparen {{ tex \texttt{(} }}
%    | )     :: :: rparen {{ tex \texttt{)} }}
%    | {     :: :: lbrace {{ tex \mbox{\tt\char'173} }}
%    | }     :: :: rbrace {{ tex \mbox{\tt\char'175} }}
%    | |     :: :: mid {{ tex \texttt{|} }}
%    | =     :: :: eq {{ tex \,\texttt{=}\, }}
%    | ,     :: :: comma {{ tex \texttt{,} }}
%    | .     :: :: dot {{ tex \texttt{.} }}
%    | :     :: :: colon {{ tex \texttt{:} }}
%    | :=     :: :: coloneq {{ tex \,\texttt{:=}\, }}

%    | array_shift :: :: array_shift {{ tex \ottkw{array\_shift} }}
%    | member_shift :: :: member_shift {{ tex \ottkw{member\_shift} }}
%    | is_scalar   :: :: is_scalar   {{ tex \ottkw{is\_scalar} }}
%    | is_integer  :: :: is_integer  {{ tex \ottkw{is\_integer} }}
%    | is_signed   :: :: is_signed   {{ tex \ottkw{is\_signed} }}
%    | is_unsigned :: :: is_unsigned {{ tex \ottkw{is\_unsigned} }}
%    | ptrdiff :: :: Ptrdiff {{ tex \ottkw{ptrdiff} }}
%    | intFromPtr :: :: IntFromPtr {{ tex \ottkw{intFromPtr} }}
%    | ptrFromInt :: :: PtrFromInt {{ tex \ottkw{ptrFromInt} }}
%    | ptrValidForDeref :: :: PtrValidForDeref {{ tex \ottkw{ptrValidForDeref} }}

% substitutions

% % single pexpr_ tyvarsym :: subst
% % multiple pexpr_ tyvarsym :: msubst

embed {{ lem
(* TODO: don't we need any impl proc? *)
type mu_impl_decl 'bty =
  | Def of base_type * mu_pexpr 'bty Symbol.sym
  | IFun of base_type * list (Symbol.sym * base_type) * mu_pexpr 'bty Symbol.sym
type mu_impl 'bty = map Implementation_.implementation_constant (mu_impl_decl 'bty)

type mu_fun_map_decl 'bty 'a =
  | Fun of base_type * list (Symbol.sym * base_type) * mu_pexpr 'bty Symbol.sym
  | Proc of Loc.t * base_type * list (Symbol.sym * base_type) * mu_expr 'a 'bty Symbol.sym
  | ProcDecl of Loc.t * base_type * list base_type
  | BuiltinDecl of Loc.t * base_type * list base_type

type mu_fun_map 'bty 'a = map Symbol.sym (mu_fun_map_decl 'bty 'a)

instance forall 'bty 'sym. (Located (mu_pattern 'bty 'sym))
  let locOf pat =
    let (Pattern annots _) = pat in
    get_loc_ annots
end

instance forall 'bty 'sym. (Located (mu_pexpr 'bty 'sym))
  let locOf pe =
    let (Pexpr annots _ _) = pe in
    get_loc_ annots
end

instance forall 'a 'bty 'sym. (Located (mu_expr 'a 'bty 'sym))
  let locOf e =
    let (Expr annots _) = e in
    get_loc_ annots
end

type linking_kind =
  | LK_none
  | LK_tentative of Symbol.sym
  | LK_normal of Symbol.sym

instance (Show linking_kind)
 let show x = 
   match x with
   | LK_none -> "LK_none"
   | LK_tentative sym -> "LK_tentative" ^ show sym
   | LK_normal sym -> "LK_normal" ^ show sym
   end
end


type extern_map =
  map Cabs.cabs_identifier (list Symbol.sym * linking_kind)

type mu_globs 'a 'bty=
  | GlobalDef of base_type * mu_expr 'a 'bty Symbol.sym
  | GlobalDecl of base_type

type mu_globs_map 'a 'bty =
  map Symbol.sym (mu_globs 'a 'bty)

type tag_definitions =
  map Symbol.sym Core_ctype.tag_definition

(* a Core file is just a set of named functions *)
type mu_file 'bty 'a = <|
  main    : maybe Symbol.sym;
  tagDefs : tag_definitions;
  stdlib  : mu_fun_map 'bty 'a;
  impl    : mu_impl 'bty;
  globs   : list (Symbol.sym * mu_globs 'a 'bty);
  funs    : mu_fun_map 'bty 'a;
  extern  : extern_map;
  funinfo : map Symbol.sym (ctype * list (maybe Symbol.sym * ctype) * bool * bool);
|>


type name            = mu_name Symbol.sym
type object_value    = mu_object_value Symbol.sym
type loaded_value    = mu_loaded_value Symbol.sym
type value           = mu_value Symbol.sym
type ctor            = mu_ctor unit
type pattern         = mu_pattern unit Symbol.sym
type pexpr           = mu_pexpr unit Symbol.sym
type expr 'a         = mu_expr 'a unit Symbol.sym
type action 'a       = mu_action 'a unit Symbol.sym
type paction 'a      = mu_paction 'a unit Symbol.sym
type impl_decl       = mu_impl_decl unit
type impl            = mu_impl unit
type fun_map_decl 'a = mu_fun_map_decl unit 'a
type fun_map 'a      = mu_fun_map unit 'a
type file 'a         = mu_file unit 'a

type typed_ctor            = mu_ctor base_type
type typed_pattern         = mu_pattern base_type Symbol.sym
type typed_pexpr           = mu_pexpr base_type Symbol.sym
type typed_expr 'a         = mu_expr 'a base_type Symbol.sym
type typed_action 'a       = mu_action 'a base_type Symbol.sym
type typed_paction 'a      = mu_paction 'a base_type Symbol.sym
type typed_impl_decl       = mu_impl_decl base_type
type typed_impl            = mu_impl base_type
type typed_fun_map_decl 'a = mu_fun_map_decl base_type 'a
type typed_fun_map 'a      = mu_fun_map base_type 'a
type typed_file 'a         = mu_file base_type 'a



declare {coq} rename type impl = impl_ (* workaround for Lem issue #74 *)






 
 
 
 
 
 (* runtime stuff *)
type continuation_element 'a =
  | Kunseq of list annot * list (expr 'a) * list (expr 'a)
  | Kwseq  of list annot * pattern * expr 'a
  | Ksseq  of list annot * pattern * expr 'a
 (*  | Ktry   list (string * expr 'a) *)

type continuation 'a = list (continuation_element 'a)
(* type labeled_continuation 'a = list (Symbol.sym * ctype) * expr 'a *)
type labeled_continuations 'a = map Symbol.sym (list Symbol.sym * expr 'a)


type stack 'a =
  | Stack_empty
    (* the symbol is that of the procedure being executed *)
  | Stack_cons of maybe Symbol.sym * (*labeled_continuations 'a * *) continuation 'a * stack 'a
 
 
 
 
 
 
 (* basic class instanciations *)
 instance (SetType polarity)
   let setElemCompare = defaultCompare
 end
}}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% the following is some placeholder setup for type judgements, just to set up the Ott basics; it's not adapted to Core

grammar

  terminals :: 'terminals_' ::=
    | \                   ::   :: lambda     {{ tex \lambda }}
    | -->                 ::   :: red        {{ tex \longrightarrow }}
    |  ->                 ::   :: arrow      {{ tex \rightarrow }}
    | |-                  ::   :: turnstile  {{ tex \vdash }}
    | in                  ::   :: in         {{ tex \in }}
    | pi :: :: pi {{ tex \Pi }}
    | forall :: :: forall {{ tex \forall }}
    | --o :: :: lollipop {{ tex \multimap }}
    | -)  :: :: implies {{ tex \supset }}
    | sigma :: :: sigma {{ tex \Sigma }}
    | exists :: :: exists {{ tex \exists }}
    | * :: :: star {{ tex \star }}
    | /\ :: :: and {{ tex \wedge }}
    | neg :: :: neg {{ tex \neg }}
    | = :: :: eq {{ tex = }}


  bt :: '' ::=           {{ ocaml 'bt }}
                         {{ phantom   }}
                         {{ com OCaml type variable for base types }}
    | _bt :: :: BaseType {{ ocaml bt  }}
                         {{ tex       }}

  ocaml_bool :: '' ::=  {{ ocaml bool }}
       	                {{ phantom    }}
    | true  :: :: True  {{ ocaml true }}
    | false :: :: False {{ ocaml false }}


  ocaml_int :: '' ::= {{ ocaml int }} {{ phantom }}
    | _int  :: :: Int {{ tex k^{ \textrm{ th } } }}


  lit :: 'Lit_' ::=
    | tyvarsym :: :: Sym
    | Unit :: :: Unit
    | ocaml_bool :: :: Bool
  

  bool_op :: '' ::=      {{ auxparam 'bt }}
    | neg index_term :: :: Not
    | index_term1 = index_term2 :: :: Eq
    | /\ ( index_term1 , .. , index_termn ) :: :: And


  list_op :: '' ::= {{ auxparam 'bt }}
    | [ index_term1 , .. , index_termn ] :: :: List
    | ocaml_int index_term :: :: NthList


  index_term_aux :: '' ::= {{ auxparam 'bt }}
    | bool_op :: :: Bool_op
    | list_op :: :: List_op


  index_term :: '' ::= {{ auxparam 'bt }}
    | lit :: :: Lit
    | index_term_aux bt :: :: IT
    | ( index_term ) :: S :: Paren {{ com parentheses }} {{ ocaml [[index_term]] }}
    | index_term [ index_term1 / tyvarsym1 , .. , index_termn / tyvarsymn ] :: M :: Subs {{ ocaml TODO }}


  arg :: ArgTy_ ::= {{ com argument types }}
    | pi tyvarsym : base_type . arg :: :: Computational
    | forall tyvarsym : logSort . arg :: :: Logical
    | resource --o arg :: :: Resource
    | index_term -) arg :: :: Constraint
    | I :: :: I


  ret :: RetTy_ ::= {{ com return types }}
    | sigma tyvarsym : base_type . ret :: :: Computational
    | exists tyvarsym : logSort . ret :: :: Logical
    | resource * ret :: :: Resource
    | index_term /\ ret :: :: Constraint
    | I :: :: I

grammar
  C {{ tex \Gamma }} :: Comp_ ::= {{ com computational var env }}
    | empty                  :: :: empty
    | C , x : base_type :: :: cons


  L {{ tex \Lambda }} :: Log_ ::= {{ com logical var env }}
    | empty :: :: empty
    | L , x :: :: cons


  N {{ tex \Xi }} :: Con_ ::= {{ com constraints env }}
    | empty :: :: empty
    | N , phi :: :: cons


  formula :: 'formula_' ::=          
    | judgement           ::   :: judgement
    | not ( formula )     ::   ::  not  
    | tyvarsym : base_type in C ::   :: xInC
    | formula1 .. formulan ::  :: dots


defns
  Jtype :: '' ::= 

defn
    C ; L ; N |- mu_pexpr_aux : ret :: :: GtT :: GtT_ by


    x : base_type in C
    ------------------------------------------ :: var
    C ; L ; N |- x : sigma y  : base_type . I


    x : bool in C
    -------------------------------------------------------- :: not
    C ; L ; N |- not ( x ) : sigma y : bool . y = (neg x _bt) _bt /\ I


    % defer globals - resources  if touched


    ------------------------------------------------------ :: value_unit
    C ; L ; N |- Unit : sigma y : unit . y = Unit _bt /\ I


    ------------------------------------------------------ :: value_true
    C ; L ; N |- True : sigma y : bool . y = true _bt /\ I


    -------------------------------------------------------- :: value_false
    C ; L ; N |- False : sigma y : bool . y = false _bt /\ I


    C ; L ; N |- mu_value1 : sigma y1 : base_type . ret1 .. C ; L ; N |- mu_valuen : sigma yn : base_type . retn
    ------------------------------------------------------------------------------------ :: value_list
    C ; L ; N |- base_type [ mu_value1 , .. , mu_valuen ] : sigma y : [ base_type ] . /\ ( y1 , .. , yn ) _bt [ ] /\ I